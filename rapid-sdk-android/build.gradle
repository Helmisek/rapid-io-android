apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

android {
	compileSdkVersion 26
	buildToolsVersion "26.0.0"
	defaultConfig {
		minSdkVersion 15
		targetSdkVersion 26
		versionCode 1
		versionName libraryVersion
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
		buildConfigField "boolean", "RELEASE", isRelease? "true" : "false"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	compile 'com.google.code.gson:gson:2.8.0'

	compile 'org.java-websocket:Java-WebSocket:1.3.0'
	compile 'com.jakewharton:disklrucache:2.0.2'
	compile 'com.koushikdutta.async:androidasync:2.2.1'
	compile 'com.annimon:stream:1.1.7'

	provided 'com.android.support:recyclerview-v7:26.0.0'

	provided "android.arch.lifecycle:runtime:1.0.0-alpha7"
	provided "android.arch.lifecycle:extensions:1.0.0-alpha7"

	testCompile 'junit:junit:4.12'
	testCompile 'org.json:json:20170516'
	testCompile 'org.skyscreamer:jsonassert:1.5.0'
	testCompile "android.arch.lifecycle:runtime:1.0.0-alpha7"
	testCompile "android.arch.lifecycle:extensions:1.0.0-alpha7"

	androidTestCompile 'com.android.support:support-annotations:26.0.0'
	androidTestCompile 'com.android.support.test:runner:1.0.0'
	androidTestCompile 'com.android.support.test:rules:1.0.0'
	// Optional -- Hamcrest library
	androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}


android.libraryVariants.all { variant ->
	task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
		description "Generates Javadoc for $variant.name."

		title "Rapid.io Android API Reference"
		source = variant.javaCompile.source
		ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
		classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
		options.links("http://docs.oracle.com/javase/7/docs/api/");
		options.links("http://d.android.com/reference/");
		exclude '**/BuildConfig.java'
		exclude '**/R.java'
		exclude '**/me/nimavat/shortid/**'
		exclude '**/io/rapid/utility/**'
		exclude '**/RapidInitProvider.java'
		failOnError false
		destinationDir = file("../docs/")
	}
}

// add Maven publish script
apply from: '../publish.gradle'